ACTOR ZombieMan2 replaces ZombieMan
{
  var int user_killpoints;
  Health 20
  Radius 20
  Height 56
  Speed 12
  PainChance 200
  Monster
  +FLOORCLIP
  +DOHARMSPECIES
  SeeSound "grunt/sight"
  AttackSound "grunt/attack"
  PainSound "grunt/pain"
  DeathSound "grunt/death"
  ActiveSound "grunt/active"
  Obituary "$OB_ZOMBIE" // "%o was killed by a zombieman."
  DropItem "Clip"
  DropItem "PowerupSpawner" 10
  States
  {
  Spawn:
	POSS A 0 A_SetUserVar("user_killpoints",10)
    POSS AB 10 A_Look
    Loop
  See:
  POSS A 0 A_SetUserVar("user_killpoints",10)
    POSS AABBCCDD 4 A_Chase
    Loop
  Missile:
    POSS E 10 A_FaceTarget
	POSS E 0 A_PlaySound ("grunt/attack")
    POSS F 1 Bright A_SpawnProjectile("ScatterPShot", 34, 7, 0)
	POSS F 5
	POSS F 1 A_FaceTarget
	POSS F 0 A_PlaySound ("grunt/attack")
	POSS F 1 Bright A_SpawnProjectile("ScatterPShot", 34, 7, 0)
    POSS E 8
    Goto See
  Pain:
    POSS G 3
    POSS G 3 A_Pain
    Goto See
  Death:
	POSS H 0 ACS_NamedExecuteAlways("GetKillPoints", 0)
    POSS H 5
    POSS I 5 A_Scream
    POSS J 5 A_NoBlocking
    POSS K 5
    POSS L -1
    Stop
  XDeath:
	POSS M 0 ACS_NamedExecuteAlways("GetKillPoints", 0)
    POSS M 5
    POSS N 5 A_XScream
    POSS O 5 A_NoBlocking
    POSS PQRST 5
    POSS U -1
    Stop
  Raise:
    POSS K 5
    POSS JIH 5
    Goto See
  }
}